buildscript {
    repositories {
        mavenCentral()
	jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
	classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.0'
    }
}

apply plugin: 'forge'
apply plugin: "jacoco"

version = "0.1"
group= "it.admiral0.minecraftantani" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "MinecraftAntani"

minecraft {
    version = "1.7.10-10.13.4.1492-1.7.10"
    runDir = "root"
}


def deps = [
    jetty: '9.3.3.v20150827',
    jersey: '2.21',
    reflections: '0.9.10'
]

dependencies {
    compile 'org.eclipse.jetty:jetty-server:' + deps.jetty
    compile 'org.eclipse.jetty:jetty-servlet:' + deps.jetty
    compile 'org.eclipse.jetty:jetty-continuation:' + deps.jetty
    
    compile 'org.glassfish.jersey.containers:jersey-container-servlet-core:' + deps.jersey
    compile 'org.glassfish.jersey.ext:jersey-mvc-mustache:' + deps.jersey
    compile 'org.glassfish.jersey.media:jersey-media-json-jackson:' + deps.jersey

    
    compile 'org.reflections:reflections:' + deps.reflections
    
    testCompile 'org.testng:testng:6.9.6'
    testCompile 'org.mockito:mockito-core:1.10.19'
}

processResources
{
    dependsOn ':launcher:shadowJar'
    
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
    from {
        rename '.*jar', 'launcher.jar'
        project(':launcher').tasks.getByName('shadowJar').outputs.files.singleFile 
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven'

    group = 'com.skcraft'
    version = '4.3-SNAPSHOT'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    repositories {
        mavenCentral()
        maven { url "http://repo.maven.apache.org/maven2" }
    }

    if (JavaVersion.current().isJava8Compatible()) {
        // Java 8 turns on doclint which we fail
        tasks.withType(Javadoc) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }
}

wrapper {
    gradleVersion '2.7'
}

jacoco {
    toolVersion '0.7.5.201505241946'
}

jacocoTestReport {
    reports {
        xml.enabled true
    }
    afterEvaluate {
        classDirectories = files(classDirectories.files.collect {
            fileTree(dir: it, include: 'it/admiral0/minecraftantani/jersey/**')
        })
    }
}

test {
    useTestNG {
        useDefaultListeners = true
    }
    testLogging.showStandardStreams = true
}



jar {
    manifest {
        attributes(
            "Implementation-Version" : version
        )
    }
}

/* 
 * Forge Gradle does funky stuff with sources so 2 copy tasks are necessary
 *  1. Generate/Expand sources to generatedSources so IDE is happy.
 *  2. Move sources around so compiler(compileJava) is happy.
 */
task generateSources(type: Copy){
    from(sourceSets.main.allSource) {
        include '**/*.in'
    }
    into "$buildDir/generatedSources";
    expand([
        'version': version
    ])
    rename '(.*)\\.in$', '$1'
}
task prepareGeneratedCompile(type: Copy){
    from "$buildDir/generatedSources"
    into "$buildDir/source/java"
}
sourceSets.main.java.srcDir "$buildDir/generatedSources"
sourceMainJava.dependsOn generateSources
sourceMainJava.finalizedBy prepareGeneratedCompile
